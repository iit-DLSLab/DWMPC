# ==============================================================================
# Generation of messages
# ==============================================================================
# Generate message files
#
# This command generates a new shared library named
# $dls_messages.  The library is automatically installed to the
# correct location in the filesystem. The header files are also installed as
# required. This allows 3rd party applications to be built such that they may
# use the messages generated by the current project.

function(dls_add_message msg)

	set(generated_source
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}.cxx"
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}PubSubTypes.cxx"
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}TypeObject.cxx"
	)
	set(generated_headers
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}.h"
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}PubSubTypes.h"
		"${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds/${msg}TypeObject.h"
	)

	# use fastddsgen to generate the message source and header files
	add_custom_command(
		OUTPUT
			${generated_source}
			${generated_headers}
		COMMAND
			[ -d ${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds ] || mkdir --parents ${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds
		COMMAND
			fastddsgen -typeobject -replace ${CMAKE_CURRENT_SOURCE_DIR}/idls/${msg}.idl -d ${CMAKE_CURRENT_BINARY_DIR}/include/dls_messages/dds
		COMMENT
			"Generating message files for ${msg}.idl"
		DEPENDS
			${CMAKE_CURRENT_SOURCE_DIR}/idls/${msg}.idl
	)

	# add the generated source files to the library
	target_sources(${MSGS_LIBRARY_NAME}
		PRIVATE
		${generated_source}
	)

	install(
		FILES 
			${generated_headers}
		DESTINATION 
			/usr/include/dls_messages/dds
		COMPONENT 
			${MSGS_LIBRARY_NAME}_dev
	)
endfunction()
